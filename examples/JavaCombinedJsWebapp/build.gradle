import org.gradle.api.invocation.Gradle;

/**
 * Copyright (C) 2014 SÃ¶nke Sothmann
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
	repositories {
		mavenCentral()
		maven {
			url new File(projectDir.parentFile.parentFile, 'repo').toURI()
		}
	}
	dependencies {
		classpath 'de.richsource.gradle.plugins:resource-caching-gradle-plugin:0.2-SNAPSHOT'
		classpath 'com.eriwen:gradle-js-plugin:1.12.0'
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'js'
apply plugin: 'resource-caching'

dependencies {
    providedCompile "javax.servlet:servlet-api:2.5"
}

combineJs {
    source = 'src/main/js'
    dest = file("${buildDir}/combined/combined.js")
}

task hashFiles(type: de.richsource.gradle.plugins.resourcecaching.HashFiles) {
  source = combineJs
  dest = file("$buildDir/hashed")
}

task generateHostPage(type: de.richsource.gradle.plugins.resourcecaching.GenerateHostPage) {
  source = hashFiles
  dest = file("$buildDir/index_html/index.html")
  template = file('src/main/webapp/index.html.template')
}

war {
  archiveName "app.war"
  includeEmptyDirs = false
  from generateHostPage
  into("js") {
    from hashFiles
  }
  exclude 'index.html.template'
}